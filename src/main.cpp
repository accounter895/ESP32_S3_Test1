#include <Arduino.h>
#include <U8g2lib.h>
#include <Wire.h>
#include <PubSubClient.h>
#include <AsyncTCP.h>
#include <ESPAsyncWebServer.h>
#include "Ticker.h"

/****** WIFIå’ŒWebè®¾ç½® ******/
AsyncWebServer server(80);
// ä½¿ç”¨ç«¯å£å·80å¯ä»¥ç›´æ¥è¾“å…¥IPè®¿é—®ï¼Œä½¿ç”¨å…¶å®ƒç«¯å£éœ€è¦è¾“å…¥IP:ç«¯å£å·è®¿é—®
// ä¸€ä¸ªå‚¨å­˜ç½‘é¡µçš„æ•°ç»„
const char index_html[] PROGMEM = R"rawliteral(
<!DOCTYPE HTML>
<html>
<head>
    <meta charset="utf-8">
    <title>è‡ªè¡Œè½¦æ™ºèƒ½ç è¡¨</title>
    <style>
        body {
            font-family: Arial, sans-serif;
            background-color: #f8f8f8;
            color: #333;
            max-width: 400px;
            margin: 20px auto;
            padding: 20px;
            box-shadow: 0 0 10px rgba(0,0,0,0.1);
            border-radius: 10px;
        }
        
        h2 {
            color: #0066cc;
            text-align: center;
            margin-top: 0;
            padding-bottom: 10px;
            border-bottom: 1px solid #e0e0e0;
        }
        
        .data-container {
            background-color: #ffffff;
            border-radius: 8px;
            padding: 15px;
            margin-top: 15px;
            box-shadow: 0 2px 5px rgba(0,0,0,0.05);
        }
        
        .data-item {
            padding: 8px 0;
            border-bottom: 1px solid #eee;
            display: flex;
            justify-content: space-between;
            align-items: center;
        }
        
        .data-item:last-child {
            border-bottom: none;
        }
        
        .label {
            font-weight: bold;
            color: #555;
        }
        
        .value {
            font-size: 1.1em;
            color: #0066cc;
        }
    </style>
</head>
<body>
    <h2>ğŸš² è‡ªè¡Œè½¦æ™ºèƒ½ç è¡¨</h2>
    
    <div class="data-container" id="Code_table">
        <!-- æ•°æ®å°†åœ¨è¿™é‡Œæ˜¾ç¤º -->
    </div>

<script>
function fetchData() {
  var xhttp = new XMLHttpRequest();
  xhttp.onreadystatechange = function() {
    if (this.readyState == 4 && this.status == 200) {
      document.getElementById("Code_table").innerHTML = this.responseText;
    }
  };
  xhttp.open("GET", "/Code_table", true);
  xhttp.send();
}

// é¡µé¢åŠ è½½æ—¶è·å–ä¸€æ¬¡æ•°æ®
document.addEventListener("DOMContentLoaded", function() {
  fetchData();
});

// æ¯éš”500æ¯«ç§’è·å–æœ€æ–°æ•°æ®
setInterval(fetchData, 500);
</script>
</body>
</html>)rawliteral";
// WiFiä¿¡æ¯
const char *ssid = "Redmi Note 12 Turbo";
const char *password = "12345678";

// ç™¾åº¦Iot Coreé…ç½®
const char* endpoint = "aadmljg.iot.gz.baidubce.com";
const int port = 1883; // æˆ–8883ï¼ˆSSLï¼‰
const char* clientId = "Esp32_S3";
const char* mqttUser = "thingidp@aadmljg|Esp32_S3|0|MD5";
const char* mqttPassword = "55161571ad6e28e22ca93d70072ffc82";

// è‡ªå®šä¹‰ä¸»é¢˜(å‘å¸ƒå’Œè®¢é˜…)
const char* pubTopic = "$iot/Esp32_S3/events";
const char* subTopic = "$iot/Esp32_S3/msg";
const char* Topic = "#";

WiFiClient espClient;
PubSubClient client(espClient);

/****** I2Cå¼•è„šå®šä¹‰ ******/
#define BOARD_I2C_SCL   9
#define BOARD_I2C_SDA   10

/****** å®šæ—¶å™¨ä¸­æ–­å‚æ•° ******/
Ticker ticker1;
uint16_t interrupt_time = 10;
uint8_t timer_intert_flag = 0;

/****** ç¼–ç å™¨å¼•è„šåŠå‚æ•° ******/
#define PI        3.1415
#define Ancoder_1 17
#define Ancoder_2 18
float speed_threshold = 5.0;
volatile long encoder_counter_1 = 0, encoder_counter_2 = 0;

/****** èœ‚é¸£å™¨å¼•è„š ******/
#define BUZZER_PIN 21

/****** æŒ‰é”®å¼•è„š ******/
#define BUTTON_SET_SPEED 11

/****** å‡½æ•°å£°æ˜ ******/
void timerls();
float readEncoder();
void counter_encoder_1();
void counter_encoder_2();

void checkSpeedAlarm(float current_speed);
void checkButtonPress();

void display_move_frame();
void displayWelcome();
void displaySpeed(float speed);

String Speed_Data(void);

// mqtt
void reconnect_MQTT();
void callback(char* topic, byte* payload, unsigned int length);

// åˆå§‹åŒ–OLED
U8G2_SSD1306_128X64_NONAME_F_SW_I2C u8g2(U8G2_R0, /* clock=*/ BOARD_I2C_SCL, /* data=*/ BOARD_I2C_SDA, /* reset=*/ U8X8_PIN_NONE);

// ç´¯ç§¯æ•°æ®
unsigned long total_pulses = 0;   // ç´¯è®¡è„‰å†²æ•°
float show_Speed = 0.0;                // å½“å‰é€Ÿåº¦
float total_distance = 0.0;       // ç´¯è®¡è·ç¦»
float average_speed = 0.0;        // å¹³å‡é€Ÿåº¦
unsigned long total_calories = 0; // ç´¯è®¡å¡è·¯é‡Œ
unsigned long trip_count = 0;     // ç´¯è®¡è¡Œç¨‹

void setup() {
  Serial.begin(115200);
  Serial.println("Init u8g2 ....");

  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid,password);
  if(WiFi.status() != WL_CONNECTED) {
    Serial.println("Connecting to WiFi...");
    while (WiFi.status() != WL_CONNECTED) {
      delay(500);
      Serial.print(".");
    }
  }
  Serial.println("Connected to WiFi");
  Serial.print("IP Address: ");
  Serial.println(WiFi.localIP());

  client.setServer(endpoint, port);
  client.setCallback(callback);

  server.on("/", HTTP_GET, [](AsyncWebServerRequest *request) {
    request->send_P(200, "text/html", index_html);
  });
  server.on("/Code_table", HTTP_GET, [](AsyncWebServerRequest *request) {
    request->send_P(200, "text/plain", Speed_Data().c_str());
  });
  server.begin();

  u8g2.setFont(u8g2_font_wqy12_t_gb2312);
  u8g2.begin();
  u8g2.enableUTF8Print();
  displayWelcome();

  pinMode(Ancoder_1, INPUT_PULLUP);
  pinMode(Ancoder_2, INPUT_PULLUP);
  attachInterrupt(digitalPinToInterrupt(Ancoder_1), counter_encoder_1, CHANGE);
  attachInterrupt(digitalPinToInterrupt(Ancoder_2), counter_encoder_2, CHANGE);

  pinMode(BUZZER_PIN, OUTPUT);
  digitalWrite(BUZZER_PIN, LOW);

  pinMode(BUTTON_SET_SPEED, INPUT_PULLUP);

  ticker1.attach_ms(interrupt_time, timerls);
  interrupts();
  delay(100);
}

void loop() {
  if (!client.connected()) {
    reconnect_MQTT();
  }
  client.loop();

  // æ¯éš”2ç§’å‘å¸ƒæ¶ˆæ¯
  static unsigned long lastMsg = 0;
  if (millis() - lastMsg > 2000) {
    lastMsg = millis();
    char payload[100];
    snprintf(payload, sizeof(payload),"{\"total_distance\":%.2f, \"average_speed\":%.2f \"total_calories\":%d}",+
    total_distance, average_speed, total_calories);
    client.publish(pubTopic, payload);
  }

  if (timer_intert_flag == 1) {
    timer_intert_flag = 0;
    show_Speed = readEncoder();
    displaySpeed(show_Speed);
    checkSpeedAlarm(show_Speed);
  }
  // åœ¨loop()æˆ–é€‚å½“ä½ç½®æ·»åŠ é˜ˆå€¼é™åˆ¶
  if (speed_threshold < 1.0) speed_threshold = 1.0;
  if (speed_threshold > 60.0) speed_threshold = 60.0;
  checkButtonPress();
  delay(1);
}

void checkSpeedAlarm(float current_speed) {
  if (current_speed > speed_threshold) {
    digitalWrite(BUZZER_PIN, LOW);
  } else {
    digitalWrite(BUZZER_PIN, HIGH);
  }
}

void checkButtonPress() {
  static unsigned long pressStartTime = 0;
  static boolean isLongPress = false;
  
  if (digitalRead(BUTTON_SET_SPEED) == LOW) {
    // æŒ‰é’®æŒ‰ä¸‹
    if (pressStartTime == 0) {
      pressStartTime = millis();  // è®°å½•æŒ‰ä¸‹å¼€å§‹æ—¶é—´
    } else if (millis() - pressStartTime >= 2000) { // é•¿æŒ‰ï¼ˆ1ç§’ï¼‰
      isLongPress = true;
      speed_threshold -= 0.5;  // é•¿æŒ‰å‡å°‘é˜ˆå€¼
      Serial.print("Long Press - New Speed Threshold: ");
      Serial.println(speed_threshold);
      delay(100);  // é˜²æ­¢è¿ç»­è§¦å‘
    }
  } else {
    // æŒ‰é’®é‡Šæ”¾
    if (pressStartTime > 0 && !isLongPress) {  // çŸ­æŒ‰
      // è®¡ç®—æŒ‰å‹æ—¶é•¿
      unsigned long pressDuration = millis() - pressStartTime;
      
      // å¦‚æœæŒ‰å‹æ—¶é—´å°äº1ç§’ï¼Œåˆ™è§†ä¸ºçŸ­æŒ‰
      if (pressDuration < 1000) {
        speed_threshold += 0.5;  // çŸ­æŒ‰å¢åŠ é˜ˆå€¼
        Serial.print("Short Press - New Speed Threshold: ");
        Serial.println(speed_threshold);
        delay(100);
      }
    }
    
    // é‡ç½®çŠ¶æ€
    pressStartTime = 0;
    isLongPress = false;
  }
}

/****** OLEDæ˜¾ç¤ºç›¸å…³ ******/
void display_move_frame() {
  u8g2.clearBuffer();
  u8g2.drawFrame(0, 0, 128, 64);
  u8g2.sendBuffer();
}

void displayWelcome() {
  char *str = "æ­£åœ¨å¯åŠ¨...";
  u8g2.clearBuffer();
  u8g2.drawUTF8(u8g2.getDisplayWidth() / 2 - u8g2.getUTF8Width(str) / 2,
                u8g2.getDisplayHeight() / 2 + u8g2.getMaxCharHeight() / 2, str);
  u8g2.sendBuffer();
}

void displaySpeed(float current_speed) {
  u8g2.clearBuffer();
  u8g2.setCursor(0, 16);
  u8g2.printf("Cur: %.2f km/h", current_speed);

  u8g2.setCursor(0, 32);
  u8g2.printf("Avg: %.2f km/h", average_speed);

  u8g2.setCursor(0, 48);
  u8g2.printf("Dist: %.2f km", total_distance);

  u8g2.setCursor(0, 64);
  u8g2.printf("Cal: %lu kcal", total_calories);
  
  u8g2.setCursor(64, 64);
  u8g2.printf("thre: %.2f", speed_threshold);

  u8g2.sendBuffer();
}

/****** å®šæ—¶å™¨ä¸­æ–­å‡½æ•° ******/
void IRAM_ATTR timerls() {
  timer_intert_flag = 1;
}

/****** ç¼–ç å™¨è®¡æ•° ******/
float wheel_circumference_km = (2 * PI * 0.035) / 100000; // è½®èƒåŠå¾„ ~0.035m => å‘¨é•¿çº¦ 0.22m => 0.0002199 km è¿™æ˜¯ä¸ªè®¡ç®—è½®èƒå‘¨é•¿çš„å…¬å¼

float readEncoder() {
  float pulses = (encoder_counter_1 + encoder_counter_2) / 2.0;
  float speed = pulses / 444.5 / 7.0 * 2 * PI * 3.6;

  total_pulses += (long)pulses;
  total_distance = total_pulses * wheel_circumference_km;

  float time_hours = (float)(millis()) / 3600000; // å°æ—¶å•ä½
  average_speed = total_distance / time_hours;

  // å‡è®¾ä½“é‡ä¸º70kg
  float weight_kg = 70.0;
  float MET;

  if (average_speed < 5) {  // æ…¢é€Ÿéª‘è¡Œ16
    MET = 3.5;
  } else if (average_speed < 7) {   // ä¸­é€Ÿéª‘è¡Œ 19
    MET = 6.0;
  } else {    // å¿«é€Ÿéª‘è¡Œ
    MET = 8.0;
  }

  //total_calories = (unsigned long)(MET * weight_kg * time_hours);
  total_calories = (unsigned long)(average_speed * total_distance * 50.0); // ç²—ç•¥ä¼°ç®—å…¬å¼
  encoder_counter_1 = 0;
  encoder_counter_2 = 0;

  return speed;
}

void counter_encoder_1() {
  encoder_counter_1++;
}

void counter_encoder_2() {
  encoder_counter_2++;
}

/******* æ‰“åŒ…ä¸Šä¼ ç½‘é¡µçš„æ•°æ® ******/
String Speed_Data(void){
  // å°†å…¨éƒ¨ç è¡¨æ•°æ®æ‰“åŒ…æˆJSONæ ¼å¼
  String dataBuffer = "<p>";
  dataBuffer += "<h1>ç è¡¨æ•°æ®</h1>";
  dataBuffer += "<b>å½“å‰é€Ÿåº¦:</b>";
  dataBuffer += String(show_Speed);
  dataBuffer += " km/h<br>";
  dataBuffer += "<b>å¹³å‡é€Ÿåº¦:</b>";
  dataBuffer += String(average_speed);
  dataBuffer += " km/h<br>";
  dataBuffer += "<b>æ€»é‡Œç¨‹:</b>";
  dataBuffer += String(total_distance);
  dataBuffer += " km<br>";
  dataBuffer += "<b>æ¶ˆè€—çš„å¡è·¯é‡Œ:</b>";
  dataBuffer += String(total_calories);
  dataBuffer += " kcal<br>";
  dataBuffer += "<b>å½“å‰é€Ÿåº¦é˜ˆå€¼:</b>";
  dataBuffer += String(speed_threshold);

  return dataBuffer;
}

/******* MQTTè¿æ¥ ******/
void reconnect_MQTT(){
  while (!client.connected()) {
    if (client.connect(clientId, mqttUser, mqttPassword)) {
      Serial.println("MQTT Connected");
      // è®¢é˜…ä¸»é¢˜
      client.subscribe(Topic);
    } else {
      Serial.print("MQTT Connect Failed, rc=");
      Serial.print(client.state());
      delay(5000);
    }
  }
}

/******* MQTTå›è°ƒå‡½æ•° ******/
void callback(char* topic, byte* payload, unsigned int length) {
  // å¤„ç†æ¥æ”¶åˆ°çš„æ¶ˆæ¯
  Serial.print("Message Arrived [");
  Serial.print(topic);
  Serial.print("]: ");
  for (int i = 0; i < length; i++) {
    Serial.print((char)payload[i]);
  }
  Serial.println();
}
